buildscript {
    repositories {
        maven {
            url 'https://maven.aliyun.com/repository/public'
        }
        maven {
            url 'https://files.minecraftforge.net/maven'
        }
        maven {
            url 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "1.0.0"
group= "online.flowerinsnow.nobreakeffect"
archivesBaseName = "no-break-effect"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    mappings = "stable_20"
    makeObfSourceJar = true
}

repositories {
    maven {
        url 'https://maven.aliyun.com/repository/public'
    }
    maven {
        url 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    embed('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from configurations.embed.asFileTree.files.collect {
        zipTree(it)
    }

    manifest.attributes([
            "TweakClass": "online.flowerinsnow.nobreakeffect.tweaker.NoBreakEffectTweaker"
    ])
}

mixin {
    add sourceSets.main, 'mixins.no-break-effect.refmap.json'
}
